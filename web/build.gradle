plugins {
    id "com.moowork.node" version "1.3.1"
}

dependencies {
    compile project(':domain')

    compile('org.springframework.boot:spring-boot-starter-web')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

def webappDir = "$projectDir/src/main/resources/webapp"

// npm install 과정
task appNpmInstall(type: NpmTask) {
    workingDir = file(webappDir)
    args = ['install']
}

// npm start
task npmStart(type: NpmTask) {
    workingDir = file(webappDir)
    args = ['start']
}

// npm build
task npmBuild(type: NpmTask) {
    workingDir = file(webappDir)
    args = ['run', 'build']
}

// 빌드된 결과 resources로 이동
task copyWebApp(type: Copy) {
    from "${webappDir}/build"
    into 'build/resources/main/static/.'
}

npmBuild.dependsOn appNpmInstall
copyWebApp.dependsOn npmBuild
compileJava.dependsOn copyWebApp